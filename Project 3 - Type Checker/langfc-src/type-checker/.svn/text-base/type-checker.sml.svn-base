(* langfc-src/type-checker/type-checker.sml
 *
 * COPYRIGHT (c) 2011-2015 Matthew Fluet (http://www.cs.rit.edu/~mtf)
 * All rights reserved.
 *
 * Rochester Institute of Technology
 * 4005-711,CSCI-742
 * Q20112,Q20122,S20135,S20145
 *
 * COPYRIGHT (c) 2009 Matthew Fluet (http://tti-c.org/fluet)
 * All rights reserved.
 *
 * University of Chicago
 * CMSC 22610
 * Winter 2009
 *
 * Front-end type checker in the LangF compiler (langfc).
 *)

structure TypeChecker :> TYPE_CHECKER =
struct
   structure PT = ParseTree
   structure AST = AbsSynTree
   structure Env = Environment

   structure Layout =
      struct
         open Layout
         fun prefixSpaceIfNonEmpty t =
            if isEmpty t then t else seq [space, t]
         fun optSeq (start, finish, sep) lay xs =
            if List.null xs
               then empty
            else seq [str start,
                      (mayAlign o separateRight)
                      (List.map lay xs, sep),
                      str finish]
      end

   fun toStringTyVarName tyvar =
      Layout.toString (PT.TyVarName.layout tyvar)
   fun toStringTyConName tycon =
      Layout.toString (PT.TyConName.layout tycon)
   fun toStringDaConName dacon =
      Layout.toString (PT.DaConName.layout dacon)
   fun toStringVarName var =
      Layout.toString (PT.VarName.layout var)

   local
      fun maybeElide s =
         let
            val len_s = String.size s
         in
            if len_s < 50
               then s
            else let
                    val front =
                       (CharVectorSlice.vector o CharVectorSlice.slice)
                       (s, 0, SOME 25)
                    val back =
                       (CharVectorSlice.vector o CharVectorSlice.slice)
                       (s, len_s - 20, NONE)
                 in
                    String.concat
                    [front, " ... ", back]
                 end
         end
   in
      fun toStringType ty =
         maybeElide (Layout.toString (PT.Type.layout ty))
      fun toStringPat pat =
         maybeElide (Layout.toString (PT.Pat.layout pat))
      fun toStringExp exp =
         maybeElide (Layout.toString (PT.Exp.layout exp))
      fun toStringMatchRule matchrule =
         maybeElide (Layout.toString (PT.MatchRule.layout matchrule))
      fun toStringDecl decl =
         maybeElide (Layout.toString (PT.Decl.layout decl))
   end

   fun toStringTypeAST tyAST =
      Layout.toString (AST.Type.layout tyAST)

   fun typeCheck (errStrm: ErrorStream.t,
                  prog : PT.Prog.t) : AST.Prog.t option =
      let

         (* By raising an exception upon reporting an error, we have a
          * type checker that stops after reporting the first error.
          * This makes the type checker easier to write, but doesn't
          * provide much feedback to the user.  After you have your
          * complete type checker working, consider improving your
          * type checker to continue after detecting the first error
          * and report multiple errors.
          *)
         exception TypeError
         fun addErrorAt (span, msgs) =
            (ErrorStream.addErrorAt (errStrm, span, concat ("\n" :: msgs))
             ; raise TypeError)

         (*
          * 5.1 Types
          *) 
         fun checkType (env: Env.t, ty: PT.Type.t)
                       : AST.Type.t =
            case PT.Type.node ty of
               PT.Type.T_TyFn (tyvar, res) =>
                  let
                     val tyvarAST = AST.TyVar.new (toStringTyVarName tyvar)
                     val resAST =
                        checkType
                        (Env.extend
                         (env,
                          Env.singletonTyVar (tyvar, {tyvar = tyvarAST})),
                         res)
                  in
                     AST.Type.T_TyFn (tyvarAST, resAST)
                  end
             | PT.Type.T_Fn (arg, res) =>
                  let
                     val argAST = checkType (env, arg)
                     val resAST = checkType (env, res)
                  in
                     AST.Type.T_Fn (argAST, resAST)
                  end
             | PT.Type.T_TyCon (tycon, actual_tyargs) =>
                  let
                     fun checkArity takes =
                        let
                           val given = List.length actual_tyargs
                        in
                           if given = takes
                              then ()
                           else
                              addErrorAt
                              (PT.Type.span ty,
                               ["Type constructor ",
                                toStringTyConName tycon,
                                " given ",
                                Int.toString given,
                                " type arguments but takes ",
                                Int.toString takes,
                                " type arguments.\n",
                                "  in: ", toStringType ty])
                        end
                  in
                     case Env.lookupTyCon (env, tycon) of
                        NONE =>
                           addErrorAt
                           (PT.TyConName.span tycon,
                            ["Unbound type constructor: ",
                             toStringTyConName tycon, "."])
                      | SOME (Env.TyConEnv.Type 
                              {tyvars = formal_tyvarsAST, ty = tyAST}) =>
                           let
                              val arity = List.length formal_tyvarsAST
                              val () = checkArity arity
                              val actual_tyargsAST = checkTypes (env, actual_tyargs)
                           in
                              AST.Type.substs (tyAST,
                                               ListPair.zip (actual_tyargsAST,
                                                             formal_tyvarsAST))
                           end
                      | SOME (Env.TyConEnv.Data {arity, tycon = tyconAST}) =>
                           let
                              val () = checkArity arity
                              val actual_tyargsAST = checkTypes (env, actual_tyargs)
                           in
                              AST.Type.T_TyCon (tyconAST, actual_tyargsAST)
                           end
                        end
             | PT.Type.T_TyVar tyvar =>
                  (case Env.lookupTyVar (env, tyvar) of
                      NONE =>
                         addErrorAt
                         (PT.TyVarName.span tyvar,
                          ["Unbound type variable: ",
                           toStringTyVarName tyvar, "."])
                    | SOME {tyvar = tyvarAST} =>
                         AST.Type.T_TyVar tyvarAST)
         and checkTypes (env: Env.t, tys: PT.Type.t list)
                        : AST.Type.t list =
            List.map (fn ty => checkType (env, ty)) tys

         (*
          * 5.2 Parameters
          *
          *
          * For simple binding checking:
          *  checkParam : Env.t * PT.Param.t -> Env.t
          *
          * For type checking, without producing an abstract syntax tree:
          *  checkParam : Env.t * PT.Param.t -> Env.t * (AST.Type.t -> AST.Type.t)
          *
          * For type checking and producing an abstract syntax tree:
          *  checkParam : Env.t * PT.Param.t -> Env.t * (AST.Type.t -> AST.Type.t) * AST.Param.t
          *)
         fun checkParam (env: Env.t, param: PT.Param.t)
                        : Env.t * (AST.Type.t -> AST.Type.t) =
            case PT.Param.node param of
               PT.Param.P_VarName (var, var_ty) =>
                  let
                     val var_tyAST = checkType (env, var_ty)
                     val env' =
                        Env.singletonVar (var, ())
                     val mkTy = fn res_tyAST =>
                        AST.Type.T_Fn (var_tyAST, res_tyAST)
                  in
                     (env', mkTy)
                  end
             | PT.Param.P_TyVarName tyvar =>
                  let
                     val tyvarAST = AST.TyVar.new (toStringTyVarName tyvar)
                     val env' =
                        Env.singletonTyVar (tyvar, {tyvar = tyvarAST})
                     val mkTy = fn res_tyAST =>
                        AST.Type.T_TyFn (tyvarAST, res_tyAST)
                  in
                     (env', mkTy)
                  end

         (*
          * 5.2.1 Multiple Parameters
          *
          *
          * For simple binding checking:
          *  checkParams : Env.t * PT.Param.t list -> Env.t
          *
          * For type checking, without producing an abstract syntax tree:
          *  checkParams : Env.t * PT.Param.t list -> Env.t * (AST.Type.t -> AST.Type.t)
          *
          * For type checking and producing an abstract syntax tree:
          *  checkParams : Env.t * PT.Param.t list -> Env.t * (AST.Type.t -> AST.Type.t) * AST.Param.t list
          *)
         fun checkParams (env: Env.t, params: PT.Param.t list)
                         : Env.t * (AST.Type.t -> AST.Type.t) =
            let
               val (env', mkTy) =
                  List.foldl
                  (fn (param_i, (env', mkTy)) =>
                   let
                      val (env'_i, mkTy_i) =
                         checkParam (Env.extend (env, env'), param_i)
                   in
                      (Env.extend (env', env'_i),
                       mkTy o mkTy_i)
                   end)
                  (Env.empty, fn ty => ty)
                  params
            in
               (env', mkTy)
            end

         (*
          * 5.3.1 Simple Patterns
          *
          *
          * For simple binding checking:
          *  checkSimplePat : Env.t * PT.SimplePat.t -> Env.t
          *
          * For type checking, without producing an abstract syntax tree:
          *  checkSimplePat : Env.t * AST.Type.t * PT.SimplePat.t -> Env.t
          *
          * For type checking and producing an abstract syntax tree:
          *  checkSimplePat : Env.t * AST.Type.t * PT.SimplePat.t -> Env.t * AST.SimplePat.t
          *)
         fun checkSimplePat (env: Env.t, pat: PT.SimplePat.t)
                            : Env.t =
            case PT.SimplePat.node pat of
               PT.SimplePat.P_VarName var =>
                  let
                     val env' =
                        Env.singletonVar (var, ())
                  in
                     env'
                  end
             | PT.SimplePat.P_Wild =>
                  let
                     val env' = Env.empty
                  in
                     env'
                  end

         (*
          * 5.3.2 Patterns
          *
          *
          * For simple binding checking:
          *  checkPat : Env.t * PT.Pat.t -> Env.t
          *
          * For type checking, without producing an abstract syntax tree:
          *  checkPat : Env.t * AST.Type.t * PT.Pat.t -> Env.t
          *
          * For type checking and producing an abstract syntax tree:
          *  checkPat : Env.t * AST.Type.t * PT.Pat.t -> Env.t * AST.Pat.t
          *)
         fun checkPat (env: Env.t, pat: PT.Pat.t)
                      : Env.t =
            case PT.Pat.node pat of
               PT.Pat.P_DaCon (dacon, actual_tyargs, actual_pats) =>
                  (case Env.lookupDaCon (env, dacon) of
                      NONE =>
                         addErrorAt
                         (PT.DaConName.span dacon,
                          ["Unbound data constructor: ",
                           toStringDaConName dacon, "."])
                    | SOME {dacon = daconAST, tyvars = formal_tyvarsAST,
                            arg_tys = formal_arg_tysAST, tycon = tyconAST} =>
                         let
                            val actual_tyargsAST = checkTypes (env, actual_tyargs)
                            val env' =
                               List.foldl
                               (fn (actual_pat_i, env') =>
                                let
                                   val env'_i = checkSimplePat (env, actual_pat_i)
                                in
                                   Env.extend (env', env'_i)
                                end)
                               Env.empty
                               actual_pats
                         in
                            env'
                         end)
             | PT.Pat.P_SimplePat spat =>
                  let
                     val env' = checkSimplePat (env, spat)
                  in
                     env'
                  end


         (*
          * 5.6.3 Data Constructor Declarations
          *)
         fun checkDaConDecl (env: Env.t,
                             tyvarsAST: AST.TyVar.t list,
                             tyconAST: AST.TyCon.t,
                             dacon_decl: PT.DaConName.t * PT.Type.t list)
                            : Env.t * (AST.DaCon.t * AST.Type.t list) =
            let
               val (dacon, arg_tys) =  dacon_decl
               val daconAST = AST.DaCon.new (toStringDaConName dacon)
               val arg_tysAST = checkTypes (env, arg_tys)
               val env' =
                  Env.singletonDaCon (dacon, {tyvars = tyvarsAST,
                                              arg_tys = arg_tysAST,
                                              tycon = tyconAST,
                                              dacon = daconAST})
            in
               (env',
                (daconAST, arg_tysAST))
            end
         fun checkDaConDecls (env: Env.t,
                              tyvarsAST: AST.TyVar.t list,
                              tyconAST: AST.TyCon.t,
                              dacon_decls: (PT.DaConName.t * PT.Type.t list) list)
                             : Env.t * (AST.DaCon.t * AST.Type.t list) list =
            let
               val (dacon_declsAST, env') =
                  ListExtra.mapAndFoldl
                  (fn (dacon_decl_i, env') =>
                   let
                      val (env'_i, dacon_declAST_i) =
                         checkDaConDecl (env,
                                         tyvarsAST,
                                         tyconAST,
                                         dacon_decl_i)
                   in
                      (dacon_declAST_i,
                       (Env.extend (env', env'_i)))
                   end)
                  Env.empty
                  dacon_decls
            in
               (env',
                dacon_declsAST)
            end

         (*
          * 5.4 Expressions
          *
          *
          * For simple binding checking:
          *  checkExp : Env.t * PT.Exp.t -> unit
          *
          * For type checking, without producing an abstract syntax tree:
          *  checkExp : Env.t * PT.Exp.t -> AST.Type.t
          *
          * For type checking and producing an abstract syntax tree:
          *  checkExp : Env.t * PT.Exp.t -> AST.Type.t * AST.Exp.t
          *)
         fun checkExp (env: Env.t, exp: PT.Exp.t)
                      : unit =
            case PT.Exp.node exp of
               PT.Exp.E_Fn (params, body) =>
                  let
                     val (env', mkTy) =
                        checkParams (env, params)
                     val () = checkExp (Env.extend (env, env'), body)
                  in
                     ()
                  end
             | PT.Exp.E_If (expIf, expThen, expElse) =>
                  let
                     val () = checkExp (env, expIf)
                     val () = checkExp (env, expThen)
                     val () = checkExp (env, expElse)
                  in
                     ()
                  end
             | PT.Exp.E_Orelse (expl, expr) =>
                  let
                     val () = checkExp (env, expl)
                     val () = checkExp (env, expr)
                  in
                     ()
                  end
             | PT.Exp.E_Andalso (expl, expr) =>
                  let
                     val () = checkExp (env, expl)
                     val () = checkExp (env, expr)
                  in
                     ()
                  end
             | PT.Exp.E_Constraint (body, ty) =>
                  let
                     val () = checkExp (env, body)
                     val tyAST = checkType (env, ty)
                  in
                     ()
                  end
             | PT.Exp.E_TernOp (ternop, expl, expm, expr) =>
                  let
                     val () = checkExp (env, expl)
                     val () = checkExp (env, expm)
                     val () = checkExp (env, expr)
                  in
                     ()
                  end
             | PT.Exp.E_BinOp (binop, expl, expr) =>
                  let
                     val () = checkExp (env, expl)
                     val () = checkExp (env, expr)
                  in
                     ()
                  end
             | PT.Exp.E_UnOp (unop, expo) =>
                  let
                     val () = checkExp (env, expo)
                  in
                     ()
                  end
             | PT.Exp.E_DaCon (dacon, actual_tyargs, actual_args) =>
                  (case Env.lookupDaCon (env, dacon) of
                      NONE =>
                         addErrorAt
                         (PT.DaConName.span dacon,
                          ["Unbound data constructor: ",
                           toStringDaConName dacon, "."])
                    | SOME {dacon = daconAST, tyvars = formal_tyvarsAST,
                            arg_tys = formal_arg_tysAST, tycon = tyconAST} =>
                         let
                            val actual_tyargsAST = checkTypes (env, actual_tyargs)
                            val _ = checkExps (env, actual_args)
                         in
                            ()
                         end)
             | PT.Exp.E_Apply (func, applyarg) =>
                  let
                     val () = checkExp (env, func)
                  in
                     case PT.ApplyArg.node applyarg of
                        PT.ApplyArg.A_Exp exp =>
                           let
                              val () = checkExp (env, exp)
                           in
                              ()
                           end
                      | PT.ApplyArg.A_Type ty =>
                           let
                              val tyAST = checkType (env, ty)
                           in
                              ()
                           end
                  end
             | PT.Exp.E_VarName var =>
                  (case Env.lookupVar (env, var) of
                      NONE =>
                         addErrorAt
                         (PT.VarName.span var,
                          ["Unbound variable: ",
                           toStringVarName var, "."])
                    | SOME () =>
                         ())
             | PT.Exp.E_Integer i => ()
             | PT.Exp.E_String s => ()
             | PT.Exp.E_Seq exps =>
                  let
                     val _ = checkExps (env, exps)
                  in
                     ()
                  end
             | PT.Exp.E_Let (decls, bodyExps) => 
                  let
                     val (env', declsAST) = checkDecls (env, decls)
                     val _ = checkExps (Env.extend (env, env'), bodyExps)
                  in
                     ()
                  end
             | PT.Exp.E_Case (arg, matchrules) => 
                  let
                     val () = checkExp (env, arg)
                     val () = checkMatchRules (env, matchrules)
                  in
                     ()
                  end
         (*
          * For simple binding checking:
          *  checkExps : Env.t list * PT.Exp.t -> unit list
          *
          * For type checking, without producing an abstract syntax tree:
          *  checkExps : Env.t * PT.Exp.t list -> AST.Type.t list
          *
          * For type checking and producing an abstract syntax tree:
          *  checkExps : Env.t * PT.Exp.t list -> (AST.Type.t * AST.Exp.t) list
          *)
         and checkExps (env: Env.t, exps: PT.Exp.t list)
                       : unit list =
            List.map (fn exp => checkExp (env, exp)) exps

         (*
          * 5.5 Match Rules
          *
          *
          * For simple binding checking:
          *  checkMatchRule : Env.t * PT.MatchRule.t -> unit
          *
          * For type checking, without producing an abstract syntax tree:
          *  checkMatchRule : Env.t * AST.Type.t * PT.MatchRule.t -> AST.Type.t
          *
          * For type checking and producing an abstract syntax tree:
          *  checkMatchRule : Env.t * AST.Type.t * PT.MatchRule.t -> AST.Type.t * AST.MatchRule.t
          *)
         and checkMatchRule (env: Env.t, matchrule: PT.MatchRule.t)
                            : unit =
            case PT.MatchRule.node matchrule of
               (pat, body) =>
                  let
                     val env' = checkPat (env, pat)
                     val () = checkExp (Env.extend (env, env'), body)
                  in
                     ()
                  end
         (*
          * 5.5.1 Multiple Match Rules
          *
          *
          * For simple binding checking:
          *  checkMatchRules : Env.t * PT.MatchRule.t list -> unit
          *
          * For type checking, without producing an abstract syntax tree:
          *  checkMatchRules : Env.t * AST.Type.t -> PT.MatchRule.t list -> AST.Type.t
          *
          * For type checking and producing an abstract syntax tree:
          *  checkMatchRules : Env.t * AST.Type.t -> PT.MatchRule.t list -> AST.Type.t * AST.MatchRule.t list
          *)
         and checkMatchRules (env: Env.t, matchrules: PT.MatchRule.t list)
                             : unit =
            let
               val _ = List.map (fn matchrule => checkMatchRule (env, matchrule)) matchrules
            in
               ()
            end

         (*
          * 5.6 Declarations
          *)
         and checkDecl (env: Env.t, decl: PT.Decl.t)
                       : Env.t * AST.Decl.t option =
            case PT.Decl.node decl of
               (*
                * 5.6.1 Type Declarations
                *)
               PT.Decl.D_Type (tycon, tyvars, ty) =>
                  let
                     val tyvarSet =
                        List.foldl
                        (fn (tyvar, tyvarSet) =>
                         let
                            val () =
                               if PT.TyVarName.Set.member (tyvarSet, tyvar)
                                  then
                                     addErrorAt
                                     (PT.TyVarName.span tyvar,
                                      ["Type parameter of 'type' defined multiple times.\n",
                                       "  type parameter: ", toStringTyVarName tyvar, "\n",
                                       "  type constructor: ", toStringTyConName tycon, "\n",
                                       "  in: ", toStringDecl decl])
                               else ()
                            val tyvarSet = PT.TyVarName.Set.add (tyvarSet, tyvar)
                         in
                            tyvarSet
                         end)
                         PT.TyVarName.Set.empty
                         tyvars
                     val (tyvarsAST, envTV) =
                        ListExtra.mapAndFoldl
                        (fn (tyvar, envTV) =>
                         let
                            val tyvarAST =
                               AST.TyVar.new (toStringTyVarName tyvar)
                            val envTV_i =
                               Env.singletonTyVar (tyvar, {tyvar = tyvarAST})
                         in
                            (tyvarAST,
                             Env.extend (envTV, envTV_i))
                         end)
                        Env.empty
                        tyvars
                     val tyAST = checkType (Env.extend (env, envTV), ty)
                     val env' =
                        Env.singletonTyCon
                        (tycon,
                         Env.TyConEnv.Type {tyvars = tyvarsAST,
                                            ty = tyAST})
                  in
                     (env', NONE)
                  end
             | PT.Decl.D_Data data_decls =>
                  let
                     val (tyconSet, daconSet) =
                        List.foldl (fn (data_decl, (tyconSet, daconSet)) =>
                                    let
                                       val (tycon, tyvars, dacon_decls) = data_decl
                                       val () =
                                          if PT.TyConName.Set.member (tyconSet, tycon)
                                             then
                                                addErrorAt
                                                (PT.TyConName.span tycon,
                                                 ["Type constructor of 'datatype' defined multiple times.\n",
                                                  "  type constructor: ", toStringTyConName tycon, "\n",
                                                  "  in: ", toStringDecl decl])
                                          else ()
                                       val tyconSet = PT.TyConName.Set.add (tyconSet, tycon)
                                       val tyvarSet =
                                          List.foldl
                                          (fn (tyvar, tyvarSet) =>
                                           let
                                              val () =
                                                 if PT.TyVarName.Set.member (tyvarSet, tyvar)
                                                    then
                                                       addErrorAt
                                                       (PT.TyVarName.span tyvar,
                                                        ["Type parameter of 'datatype' defined multiple times.\n",
                                                         "  type parameter: ", toStringTyVarName tyvar, "\n",
                                                         "  type constructor: ", toStringTyConName tycon, "\n",
                                                         "  in: ", toStringDecl decl])
                                                 else ()
                                              val tyvarSet = PT.TyVarName.Set.add (tyvarSet, tyvar)
                                           in
                                              tyvarSet
                                           end)
                                          PT.TyVarName.Set.empty
                                          tyvars
                                       val daconSet =
                                          List.foldl
                                          (fn (dacon_decl, daconSet) =>
                                           let
                                              val (dacon, arg_tys) = dacon_decl
                                              val () =
                                                 if PT.DaConName.Set.member (daconSet, dacon)
                                                    then
                                                       addErrorAt
                                                       (PT.DaConName.span dacon,
                                                        ["Data constructor of 'datatype' defined multiple times.\n",
                                                         "  data constructor: ", toStringDaConName dacon, "\n",
                                                         "  type constructor: ", toStringTyConName tycon, "\n",
                                                         "  in: ", toStringDecl decl])
                                                 else ()
                                              val daconSet = PT.DaConName.Set.add (daconSet, dacon)
                                           in
                                              daconSet
                                           end)
                                          daconSet
                                          dacon_decls
                                    in
                                       (tyconSet, daconSet)
                                    end)
                                  (PT.TyConName.Set.empty, PT.DaConName.Set.empty)
                                  data_decls
                     val (data_declsAux, envTC) =
                        ListExtra.mapAndFoldl
                        (fn ((tycon, tyvars, dacon_decls), envTC) =>
                         let
                            val tyconAST = AST.TyCon.new (toStringTyConName tycon)
                            val arity = List.length tyvars
                            val envTC_i =
                               Env.singletonTyConData (tycon, {arity = arity, tycon = tyconAST})
                         in
                            ((tyconAST, tyvars, dacon_decls),
                             Env.extend (envTC, envTC_i))
                         end)
                        Env.empty
                        data_decls
                     val (data_declsAST, env') =
                        ListExtra.mapAndFoldl
                        (fn ((tyconAST, tyvars, dacon_decls), env') =>
                         let
                            val (tyvarsAST, envTV_i) =
                               ListExtra.mapAndFoldl
                               (fn (tyvar, envTV) =>
                                let
                                   val tyvarAST =
                                      AST.TyVar.new (toStringTyVarName tyvar)
                                   val envTV_i =
                                      Env.singletonTyVar (tyvar, {tyvar = tyvarAST})
                                in
                                   (tyvarAST,
                                    Env.extend (envTV, envTV_i))
                                end)
                               Env.empty
                               tyvars
                            val (envDC_i, dacon_declsAST) =
                               checkDaConDecls (Env.extend (env, (Env.extend (envTC, envTV_i))),
                                                tyvarsAST,
                                                tyconAST,
                                                dacon_decls)
                         in
                            ((tyconAST, tyvarsAST, dacon_declsAST),
                             Env.extend (env', envDC_i))
                         end)
                        envTC
                        data_declsAux
                  in
                     (env',
                      SOME (AST.Decl.D_Data data_declsAST))
                  end
               (*
                * 5.6.4 Value Declarations
                *)
             | PT.Decl.D_Val (pat, constr_tyOpt, exp) =>
                  let
                     val () = checkExp (env, exp)
                     val () =
                        case constr_tyOpt of
                           NONE => ()
                         | SOME constr_ty => 
                              let
                                 val constr_tyAST = checkType (env, constr_ty)
                              in
                                 ()
                              end
                     val env' = checkSimplePat (env, pat)
                  in
                     (env',
                      NONE)
                  end
               (*
                * 5.6.4 Function Declarations
                *)
             | PT.Decl.D_Fun fun_decls => 
                  let
                     val (fun_declsAux, env') =
                        ListExtra.mapAndFoldl
                        (fn ((var, params, res_ty, body), env') =>
                         let
                            val (envP, mkTy) = checkParams (env, params)
                            val res_tyAST = checkType (Env.extend (env, envP), res_ty)
                            val env'_i = Env.singletonVar (var, ())
                         in
                            ((envP, body),
                             Env.extend (env', env'_i))
                         end)
                        Env.empty
                        fun_decls
                     val () =
                        List.app
                        (fn (envP, body) =>
                         let
                            val () = 
                               checkExp (Env.extend (env, Env.extend (env', envP)),
                                         body)
                         in
                            ()
                         end)
                        fun_declsAux
                  in 
                     (env',
                      NONE)
                  end
         and checkDecls (env: Env.t, decls: PT.Decl.t list)
                        : Env.t * AST.Decl.t list =
            let
               val (declsAST, env') =
                  ListExtra.mapAndFoldl
                  (fn (decl_i, env') =>
                   let
                      val (env'_i, declAST_i) =
                         checkDecl (Env.extend (env, env'), decl_i)
                   in
                      (declAST_i,
                       Env.extend (env', env'_i))
                   end)
                  Env.empty
                  decls
            in
               (env',
                List.mapPartial (fn declAST => declAST) declsAST)
            end

         fun checkProg (prog: PT.Prog.t) : AST.Prog.t option =
            let
               val env0 = Env.initial
               val (decls, exp) = PT.Prog.node prog
               val (env', declsAST) = checkDecls (env0, decls)
               val () = checkExp (Env.extend (env0, env'), exp)
               val progAST = AST.Prog.Prog (declsAST, AST.Exp.unit)
               (*
                * val (tyAST, expAST) = checkExp (Env.extend (env0, env'), exp)
                * val progAST = AST.Prog.Prog (declsAST, expAST)
                *)
            in
               SOME progAST
            end
            handle TypeError => NONE
      in
         checkProg prog
      end

end
